capsule PiComputer {
    [[rt::impl_preface]]
    `
        #include <iostream>
        #include <iomanip>
    `
    /* Ports */
    behavior port computer~ : PI_REQ;    
    /* Parts */
    part adder : Adder;
    part multiplier : Multiplier;
    /* Connectors */
    connect computer with adder.request_port;
    connect adder.mul with multiplier.result;
    /* State Machine */
    statemachine {
        state WaitForComputation;
        state Finished {
            entry
            `
                context()->abort();
            `;
        };
        initial -> WaitForComputation
        `
            int noOfIterations = 100;        
            std::cout << "Starting computation of pi using the Madhavas formula with " << noOfIterations << " iterations" << std::endl;
            computer.compute(noOfIterations).send();
        `;

        WaitForComputation -> Finished on computer.result
        `
            std::cout << "Result is : " << std::setprecision(20) << std::fixed << *rtdata << std::endl;
        `;
    };
};