// Representation of the push button which a pedestrian can push
// to request a safe crossing of the street.
capsule PushButton : `IPushButton` {
    [[rt::header_preface]]
    `
        #include "IPushButton.h"
    `
    [[rt::impl_preface]]
    `
        #include <iostream>
        #include "mythread.h"
    `

    [[rt::decl]]
    `
        public:
            // Called by the Qt UI when the push button is clicked
            virtual void onClicked() override;
    `

    [[rt::impl]]
    `
        void PushButton::onClicked() {
            external.raise();
        }
    `
 
    service behavior port trafficLight~ : TrafficLightControl;
    behavior port external : External;    

    statemachine {
        state WaitForIncomingMessage {
            entry
            `
                // Enable the external port so we can receive *one* more
                // event from the Qt UI button
                external.enable();
            `;
        };
        initial -> WaitForIncomingMessage
        `
            MyThread::Instance().registerPushButton(this);
        `
        ;

        onButtonPush: WaitForIncomingMessage -> WaitForIncomingMessage on external.event
        `
            // The button was pushed
            trafficLight.pedestrian().send();
        `
        ;
    };
};